group 'com.colinsurprenant'

version '0.1.0'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'jflex'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        // this uses the gradle-jflex-plugin. The original version does not support jflex 1.6.1 so
        // you can use my forked version at https://github.com/colinsurprenant/gradle-jflex-plugin
        // or the original if/when https://github.com/thomaslee/gradle-jflex-plugin/pull/2 is merged.
        //
        // clone https://github.com/colinsurprenant/gradle-jflex-plugin
        //   or
        // clone https://github.com/thomaslee/gradle-jflex-plugin
        //
        // $ gradle build
        // $ gradle install
        classpath 'co.tomlee.gradle.plugins:gradle-jflex-plugin:0.0.3-SNAPSHOT'
    }
}

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

// support for "provided" dependencies
configurations {
    provided
}

// support for "provided" dependencies
sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

// sync "provided" dependencies into intellij module dependencies
idea {
    module {
        scopes.PROVIDED.plus += [project.configurations.provided]
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    provided 'org.apache.lucene:lucene-core:5.4.1'
    provided 'org.apache.lucene:lucene-analyzers-common:5.4.1'
    compile 'de.jflex:jflex:1.6.1'
    jflex 'de.jflex:jflex:1.6.1'
}

// below handles the embedded gem jar copy/clean

task copyGemjar(type: Copy, dependsOn: classes) {
    from project.jar
    into project.file('lib/fast_fuzzy')
}

task cleanGemjar {
    delete fileTree(project.file('lib/fast_fuzzy')) {
        include '*.jar'
    }
}

clean.dependsOn(cleanGemjar)
build.finalizedBy(copyGemjar)

// See http://www.gradle.org/docs/current/userguide/gradle_wrapper.html
task wrapper(type: Wrapper) {
    description = 'Install Gradle wrapper'
    gradleVersion = '2.8'
}

